group "org.polypheny"
description = "A lightweight Polypheny-DB benchmarking client"

def versionMajor = 1
def versionMinor = 11.1
def versionQualifier = "-SNAPSHOT"
version = versionMajor + "." + versionMinor + versionQualifier


buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        // Shadowing; used to make a fat jar (https://github.com/johnrengelman/shadow)
        classpath group: "gradle.plugin.com.github.johnrengelman", name: "shadow", version: "7.1.0"
        // Lombok (https://plugins.gradle.org/plugin/io.freefair.lombok)
        classpath group: "io.freefair.gradle", name: "lombok-plugin", version: "6.2.0"
    }
}


repositories {
    mavenCentral()
    maven {
        url "https://clojars.org/repo/"
    }
    maven {
        // DBIS Nexus
        url "https://dbis-nexus.dmi.unibas.ch/repository/maven2/"
    }
}

apply plugin: "java-library"
apply plugin: "idea"
apply plugin: "io.freefair.lombok"
apply plugin: "com.github.johnrengelman.shadow"


tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}


//configurations.all {
//    // check for updates every build
//    resolutionStrategy.cacheChangingModulesFor 0, "seconds"
//}


java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}


dependencies {
    implementation group: 'commons-lang', name: 'commons-lang', version: '2.6' // License: Apache 2.0
    implementation group: "commons-io", name: "commons-io", version: '2.6'  // License: Apache 2.0

    // Polypheny Control Connector
    implementation (group: 'org.polypheny', name: 'polypheny-control', version: '1.2.2') { // License: Apache 2.0
        exclude group: 'org.gradle', module: 'gradle-tooling-api'
        exclude group: 'com.github.rvesse', module: 'airline'
        exclude group: 'io.javalin', module: 'javalin'
    }

    // Chronos
    implementation group: 'org.chronos-eaas', name: 'chronos-agent', version: '2.3.3' // License: MIT

    // REST
    implementation group: 'com.konghq', name: 'unirest-java', version: '3.7.02' // License: MIT

    // JDBC Drivers
    implementation group: 'org.polypheny', name: 'polypheny-jdbc-driver', version: '1.5.1' // License: Apache 2.0
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.1'
    implementation group: 'monetdb', name: 'monetdb-jdbc', version: '2.29' // License: Mozilla Public License 2.0

    // Cottontail-DB
    implementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.30' // License: MIT
    implementation (group: 'org.vitrivr', name: 'cottontaildb', version: '0.10.5') { // License: MIT
        exclude group: 'org.apache.logging.log4j'
        exclude group: 'org.slf4j'
    }

    // CLI library
    implementation group: 'com.github.rvesse', name: 'airline', version: '2.8.0' // License: Apache 2.0

    // Fake data generator
    implementation group: 'com.devskiller', name: 'jfairy', version: '0.6.4' // License: Apache 2.0

    // Video processing
    implementation group: 'org.jcodec', name: 'jcodec', version: '0.2.5' // License: BSD-2-Clause License
    implementation group: 'org.jcodec', name: 'jcodec-javase', version: '0.2.5' // License: BSD-2-Clause License

    // CSV
    implementation group: 'com.opencsv', name: 'opencsv', version: '5.0' // License: Apache 2.0

    // LOGGING
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30' // License: MIT
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.13.3' // License: Apache 2.0
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.3' // License: Apache 2.0

    // Testing
    testImplementation group: 'junit', name: 'junit', version: '4.13'
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
    // suppress most of the warnings
    options.addStringOption("Xdoclint:none", "-quiet")
    // Include private fields in JavaDoc
    options.memberLevel = JavadocMemberLevel.PRIVATE

    options.setOutputLevel(JavadocOutputLevel.QUIET)
    options.quiet()
}


/**
 * JARs
 */
jar {
    manifest {
        attributes "Manifest-Version": "1.0"
        attributes "Copyright": "The Polypheny Project (polypheny.org)"
        attributes "Version": project.version
        attributes "Main-Class": "org.polypheny.simpleclient.cli.Main"
        attributes "Multi-Release": "true"
        attributes "Implementation-Version": project.version
    }
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}
shadowJar {
    zip64 true
    classifier = ""
    mergeServiceFiles {
        setPath("META-INF/services")
        include("io.grpc.*")
    }
}
assemble.dependsOn shadowJar
artifacts {
    //archives jar // regular jar containing only the compiled source
    archives shadowJar // fat jar which additionally contains all dependencies
    archives sourcesJar // jar file containing the java doc files
    archives javadocJar // jar file containing the source files
}


/**
 * IntelliJ
 */
idea {
    module {
        downloadJavadoc = true
        downloadSources = true

        inheritOutputDirs = false
        outputDir = file("${project.buildDir}/classes")
        testOutputDir = file("${project.buildDir}/test-classes")
    }
}
